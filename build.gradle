plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.11' apply false
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'org.owasp.dependencycheck' version '12.1.0'
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'org.owasp.dependencycheck'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += '-parameters'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.11"
        }
        dependencies {
            dependency 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.0'
            dependency 'io.swagger.core.v3:swagger-annotations:2.2.27'
            dependency 'io.swagger.core.v3:swagger-models-jakarta:2.2.27'
            dependency 'org.webjars:swagger-ui:5.19.0'
            dependency 'com.itextpdf:barcodes:8.0.5'
            dependency 'com.itextpdf:commons:8.0.5'
            dependency 'com.itextpdf:io:8.0.5'
            dependency 'com.itextpdf:kernel:8.0.5'
            dependency 'com.itextpdf:layout:8.0.5'
            dependency 'com.itextpdf:bouncy-castle-connector:8.0.5'
            dependency 'org.apache.logging.log4j:log4j-api:2.25.3'
            dependency 'org.apache.logging.log4j:log4j-to-slf4j:2.25.3'
            dependency 'org.apache.kafka:kafka-clients:3.9.1'
            dependency 'org.apache.kafka:kafka_2.13:3.9.1'
            dependency 'org.assertj:assertj-core:3.27.7'
            dependency 'org.bitbucket.b_c:jose4j:0.9.6'
            dependency 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
        }
    }

    configurations.configureEach {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
    }

    tasks.named('test') {
        useJUnitPlatform()
        testLogging {
            events 'failed', 'skipped', 'passed'
            exceptionFormat 'full'
            showStandardStreams = true
        }
    }

    tasks.named('jacocoTestReport') {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
    }

    dependencyCheck {
        autoUpdate = true
        failOnError = false
        formats = ['HTML', 'JSON']
        suppressionFile = "$rootDir/dependency-check-suppressions.xml"
        analyzers {
            assemblyEnabled = false
            ossIndexEnabled = false
        }
    }
}

dependencyCheck {
    autoUpdate = true
    failOnError = false
    formats = ['HTML', 'JSON']
    suppressionFile = "$rootDir/dependency-check-suppressions.xml"
    analyzers {
        assemblyEnabled = false
        ossIndexEnabled = false
    }
}

sonarqube {
    properties {
    property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'devfaso'
        property 'sonar.projectKey', 'DevFaso_hospital-monorepo'
    }
}

